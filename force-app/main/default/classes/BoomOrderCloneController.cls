public with sharing class BoomOrderCloneController {
  @AuraEnabled
  public static Id cloneBoomOrder(Id recordId) {
    Savepoint sp = Database.setSavepoint();
    try {
      String editableFields = getEditableFields('SalesOrder__c');
      String soql =
        'SELECT ' +
        editableFields +
        ' FROM SalesOrder__c WHERE Id = \'' +
        recordId +
        '\' LIMIT 1';
      SalesOrder__c boomOrder = Database.query(soql);
      SalesOrder__c clonedOrder = boomOrder.clone(false, true, false, false);
      clonedOrder.Approval_Status__c = 'Pending';
      clonedOrder.Status_Picklist__c = 'Draft';
      clonedOrder.Release_Status__c = null;
      clonedOrder.DateReleased__c = null;
      clonedOrder.DateApproved__c = null;
      clonedOrder.DateCancelled__c = null;
      clonedOrder.DateSent__c = null;
      clonedOrder.Release_Date__c = null;
      clonedOrder.OrderDate__c = Date.today();
      clonedOrder.Released__c = null;
      insert clonedOrder;
      cloneOrderProductLines(recordId, clonedOrder.Id);
      cloneAdditionalChargeCredit(recordId, clonedOrder.Id);
      return clonedOrder.Id;
    } catch (Exception e) {
      Database.rollback(sp);
      throw new AuraHandledException(e.getMessage());
    }
  }

  public static void cloneAdditionalChargeCredit(
    Id oldBoomOrderId,
    Id newBoomOrderId
  ) {
    Savepoint sp = Database.setSavepoint();
    try {
      String editableFields = getEditableFields(
        'SalesOrderAdditionalChargeCredit__c'
      );
      String soql =
        'SELECT ' +
        editableFields +
        ' FROM SalesOrderAdditionalChargeCredit__c WHERE SalesOrder__c = \'' +
        oldBoomOrderId +
        '\'';
      List<SalesOrderAdditionalChargeCredit__c> oldACCs = Database.query(soql);
      if (oldACCs.size() > 0) {
        List<SalesOrderAdditionalChargeCredit__c> newACCs = new List<SalesOrderAdditionalChargeCredit__c>();
        for (SalesOrderAdditionalChargeCredit__c oA : oldACCs) {
            SalesOrderAdditionalChargeCredit__c nA = oA.clone(false, true, false, false);
            nA.SalesOrder__c = newBoomOrderId;
            newACCs.add(nA);
        }
        insert newACCs;
      }
    } catch (Exception e) {
      Database.rollback(sp);
      throw new AuraHandledException(e.getMessage());
    }
  }

  public static void cloneOrderProductLines(
    Id oldBoomOrderId,
    Id newBoomOrderId
  ) {
    Savepoint sp = Database.setSavepoint();
    try {
      String editableFields = getEditableFields('SalesOrderProductLine__c');
      String soql =
        'SELECT ' +
        editableFields +
        ', (SELECT Id, Recipient__c, Primary__c FROM Order_Line_Recipients__r)' +
        ' FROM SalesOrderProductLine__c WHERE SalesOrder__c = \'' +
        oldBoomOrderId +
        '\'';
      List<SalesOrderProductLine__c> oldOrderProductLines = Database.query(
        soql
      );
      if (oldOrderProductLines.size() > 0) {
        Map<Id, SalesOrderProductLine__c> clonedOrderProductLines = new Map<Id, SalesOrderProductLine__c>();
        Map<Id, Order_Line_Recipient__c> oldOrderLineRecipients = new Map<Id, Order_Line_Recipient__c>();
        for (
          SalesOrderProductLine__c oldOrderProductLine : oldOrderProductLines
        ) {
          if (oldOrderProductLine.KitBundleLine__c == null) {
            SalesOrderProductLine__c clonedOrderProductLine = oldOrderProductLine.clone(
              false,
              true,
              false,
              false
            );
            
            oldOrderLineRecipients.put(
              oldOrderProductLine.Id,
              oldOrderProductLine.Order_Line_Recipients__r
            );
            String clonedJson = JSON.serialize(clonedOrderProductLine);
            clonedJson =
              clonedJson.substringBefore(',"Order_Line_Recipients__r"') +
              clonedJson.substringAfter(']');
            
            clonedOrderProductLine = (SalesOrderProductLine__c) JSON.deserialize(
              clonedJson,
              SalesOrderProductLine__c.class
            );
            clonedOrderProductLine.SalesOrder__c = newBoomOrderId;
            clonedOrderProductLine.Recipient_Count__c = 0;
            clonedOrderProductLine.DateRequired__c = Date.today();
            clonedOrderProductLines.put(
              oldOrderProductLine.Id,
              clonedOrderProductLine
            );
          }
        }
        insert clonedOrderProductLines.values();
        List<Order_Line_Recipient__c> autoGeneratedRecipients = [
          SELECT Id
          FROM Order_Line_Recipient__c
          WHERE OrderProductLine__c = :clonedOrderProductLines.values()
        ];

        // delete automatically generated recipients generated by trigger
        delete autoGeneratedRecipients;
        List<Order_Line_Recipient__c> newRecipients = new List<Order_Line_Recipient__c>();
        for (Id oldId : clonedOrderProductLines.keySet()) {
          SalesOrderProductLine__c clonedPL = clonedOrderProductLines.get(
            oldId
          );
          Order_Line_Recipient__c newRecipient = new Order_Line_Recipient__c();
          newRecipient.Recipient__c = oldOrderLineRecipients.get(oldId).Recipient__c;
          newRecipient.Order__c = newBoomOrderId;
          newRecipient.OrderProductLine__c = clonedPL.Id;
          newRecipient.Primary__c = oldOrderLineRecipients.get(oldId)
            .Primary__c;
          newRecipients.add(newRecipient);
        }
        insert newRecipients;
      }
    } catch (Exception e) {
      Database.rollback(sp);
      throw new AuraHandledException(e.getMessage());
    }
  }

  public static String getEditableFields(String sobjectName) {
    Map<String, Schema.SObjectField> fields = Schema.getGlobalDescribe()
      .get(sobjectName)
      .getDescribe()
      .fields.getMap();
    String editableFields = '';
    for (Schema.SObjectField fieldRef : fields.values()) {
      Schema.DescribeFieldResult fieldResult = fieldRef.getDescribe();
      if (fieldResult.isUpdateable()) {
        editableFields += fieldResult.getname() + ',';
      }
    }
    editableFields = editableFields.removeEnd(',');
    return editableFields;
  }
}